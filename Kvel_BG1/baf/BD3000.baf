
IF
    InParty("C0KVEL")
    Global("C0KvelSoDAndroszAlly","GLOBAL",1)
    Global("C0KvelSoDAndroszAllySpawn","MYAREA",0)
THEN
    RESPONSE #100
        SetGlobal("C0KvelSoDAndroszAllySpawn","MYAREA",1)
        CreateCreature("C02ANDRO",[1198.134],S)
        GiveItemCreate("C0KVHE01","C02ANDRO",1,0,0)
	    ActionOverride("C02ANDRO",XEquipItem("C0KVHE01",Myself,SLOT_HELMET,EQUIP))
END

IF
    InMyArea("C02ANDRO")
    OR(6)
	NearLocation(Player1,1212,2354,30)
	NearLocation(Player2,1212,2354,30)
	NearLocation(Player3,1212,2354,30)
	NearLocation(Player4,1212,2354,30)
	NearLocation(Player5,1212,2354,30)
	NearLocation(Player6,1212,2354,30)
    GlobalLT("C0KvelSoDAndroszEvent","GLOBAL",3)
THEN
RESPONSE #100
	SetInterrupt(FALSE)
	SetGlobal("C0KvelSoDAndroszEvent","GLOBAL",3)
    ClearAllActions()
    StartCutSceneMode()
    StartCutScene("C02KVC03")
	SetInterrupt(TRUE)
END

IF
    GlobalGT("bd_plot","GLOBAL",393)
    Global("C0KvelSoDAndroszAllySpawn","MYAREA",1)
THEN
RESPONSE #100
	SetInterrupt(FALSE)
	SetGlobal("C0KvelSoDAndroszAllySpawn","MYAREA",2)
    ActionOverride("C02ANDRO",DestroySelf())
	SetInterrupt(TRUE)
END

IF
  Global("C0KvelSoDSpawn","GLOBAL",1)
  Global("C0Kvel_MoveCamp","MYAREA",0)
  !Dead("C0Kvel")
  !InPartyAllowDead("C0Kvel")
THEN
  RESPONSE #100
    SetGlobal("C0Kvel_MoveCamp","MYAREA",1)
    MoveGlobal("bd3000","C0Kvel",[1121.191]) 
    ActionOverride("C0Kvel",Face(NE))
    ReallyForceSpellDeadRES("bdrejuve","C0Kvel")
    ChangeEnemyAlly("C0Kvel",NEUTRAL)
    ChangeSpecifics("C0Kvel",ALLIES)
    ActionOverride("C0Kvel",SetGlobal("bd_joined","locals",0))
    ActionOverride("C0Kvel",SetGlobal("bd_retreat","locals",0))
    ActionOverride("C0Kvel",SaveObjectLocation("LOCALS","bd_default_loc",Myself))
    ActionOverride("C0Kvel",ChangeAIScript("C02KVEL",OVERRIDE))
    ActionOverride("C0Kvel",ChangeAIScript("bdasc3",CLASS))
    ActionOverride("C0Kvel",ChangeAIScript("BDSHOUT",RACE))
    ActionOverride("C0Kvel",ChangeAIScript("BDMAGE01",GENERAL))
    ActionOverride("C0Kvel",ChangeAIScript("",DEFAULT))
    ActionOverride("C0Kvel",SetDialogue("C02KVEL"))
    Continue()
END