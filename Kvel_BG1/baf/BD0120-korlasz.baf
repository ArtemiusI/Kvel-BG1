/* bd0120_commenting.bafC02_Saved_Kvel
In Korlasz' Crypt: NPC state their plans after the last follower of Sarevok will be defeated.
This will be patched to both bd0120.bcs and bd0130.bcs. */

/* First comment: before Korlasz will be defeated. */
IF
	Global("C0Kvel_SoDleavingcomment","global",0)
	CombatCounter(0)
	GlobalGT("bd_leaving_ot","GLOBAL",0)
	GlobalLT("bd_leaving_ot","GLOBAL",7)
	IsValidForPartyDialogue("C0Kvel")  
	GlobalTimerExpired("bd_leaving_ot_timer","global")
	!Dead("bdkorlas")  // Korlasz
	!Global("BD_KORLASZ_SURRENDER","GLOBAL",1)
THEN
	RESPONSE #200
		Continue()
	RESPONSE #100
		IncrementGlobal("bd_leaving_ot","global",1)
		SetGlobal("C0Kvel_SoDleavingcomment","global",1)
		SetGlobalTimerRandom("bd_leaving_ot_timer","global",TWENTY_MINUTES,THIRTY_MINUTES)
		DisplayStringHead("C0Kvel",~May this be the end of this overly long conflict.~)
END

/* Second comment: after Korlasz defeated */
IF
	GlobalLT("C0Kvel_SoDleavingcomment","global",2)
	CombatCounter(0)
	GlobalGT("bd_leaving2_ot","GLOBAL",0)
	GlobalLT("bd_leaving2_ot","GLOBAL",7)
	IsValidForPartyDialogue("C0Kvel")  
	GlobalTimerExpired("bd_leaving2_ot_timer","global")
	OR(2)
		Dead("bdkorlas")  // Korlasz
		Global("BD_KORLASZ_SURRENDER","GLOBAL",1)
THEN
	RESPONSE #200
		Continue()
	RESPONSE #100
		BanterBlockTime(100)
		IncrementGlobal("bd_leaving2_ot","global",1)
		SetGlobal("C0Kvel_SoDleavingcomment","global",2)
		SetGlobalTimerRandom("bd_leaving2_ot_timer","global",EIGHT_MINUTES,THIRTEEN_MINUTES)
		DisplayStringWait("C0Kvel",~As you may be free from conflict awhile, I will depart from your immediate presence soon. Though I will remain in the city, should you require my continued service.~)  
END