IF
	InParty(Myself)
	Global("C0KvelName","LOCALS",0)
THEN
RESPONSE #100	
	SetGlobal("C0KvelName","LOCALS",1)
	SetName(~K'Vel~)
	Continue()
END

IF
    HasItemEquipedReal("IMOENHP1",Myself) 
	OR(2)
    InParty(Myself)
	Allegiance(Myself,ENEMY)
THEN
    RESPONSE #100
        DestroyItem("IMOENHP1") 
        Continue()
END

IF
    Allegiance("C0KVNP01",ENEMY)
	InParty(Myself)
THEN
    RESPONSE #100
		ChangeAIScript("BDMAGE01",DEFAULT)
        LeaveParty()
		Enemy()
END

IF
	Allegiance(Myself,NEUTRAL)
    Allegiance("C0KVNP01",ENEMY)
	!InParty(Myself)
THEN
    RESPONSE #100
		ChangeAIScript("BDMAGE01",DEFAULT)
		Enemy()
END

// EXPERIENCE

IF
    Global("BD_JOINXP","LOCALS",0)
    InParty(Myself)
    XPGT(Player1,95999)
    XPLT(Myself,96000)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobal("BD_JOINXP","LOCALS",1)
        ChangeStat(Myself,XP,96000,SET)
        SetInterrupt(TRUE)
END

IF
    Global("BD_JOINXP","LOCALS",0)
    InParty(Myself)
    XPGT(Player1,47999)
    XPLT(Myself,48000)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobal("BD_JOINXP","LOCALS",1)
        ChangeStat(Myself,XP,48000,SET)
        SetInterrupt(TRUE)
END

IF
    Global("BD_JOINXP","LOCALS",0)
    InParty(Myself)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobal("BD_JOINXP","LOCALS",1)
        SetInterrupt(TRUE)
END


// OUTDOORS

IF
	InParty(Myself)
	AreaType(OUTDOOR)
	OR(2)
	TimeOfDay(DAY)
	TimeOfDay(MORNING)
	Global("C0KvelOutsideFirstTime","GLOBAL",0)
THEN
RESPONSE #100
	Wait(1)
	SetGlobal("C0KvelOutsideFirstTime","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// ANGEL THE CAT

IF
	InParty(Myself)
	See("PetrinesCat")
	Global("C0KvelPetrinesCat","GLOBAL",0)
THEN
RESPONSE #100
	Wait(1)
	SetGlobal("C0KvelPetrinesCat","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// ANGELO TORTURE

IF
	See([PC])
	Global("C0KvelAngeloTorture","GLOBAL",3)
THEN
RESPONSE #100
	SetInterrupt(FALSE)
	Dialogue(Player1)
	SetInterrupt(TRUE)
END

// FLAMING FIST

IF
	InParty(Myself)
	Global("C0KvelFlamingFistC7Kill","GLOBAL",1)
	CombatCounter(0)
THEN
RESPONSE #100
	SetInterrupt(FALSE)
	Wait(1)
	SetGlobal("C0KvelFlamingFistC7Kill","GLOBAL",2)
	SetInterrupt(TRUE)
END

// JHAN

IF
    Global("EnteredElderCity","GLOBAL",0)
    GlobalGT("Chapter","GLOBAL",6)
    Global("C0KvelBG1Event","GLOBAL",0)
    OR(9)
    AreaCheck("%bg1_eet_symbol%0100")
    AreaCheck("%bg1_eet_symbol%0200")
    AreaCheck("%bg1_eet_symbol%0300")
    AreaCheck("%bg1_eet_symbol%0600")
    AreaCheck("%bg1_eet_symbol%0700")
    AreaCheck("%bg1_eet_symbol%0800")
    AreaCheck("%bg1_eet_symbol%1100")
    AreaCheck("%bg1_eet_symbol%1200")
    AreaCheck("%bg1_eet_symbol%1300")
THEN
    RESPONSE #100
        SetGlobal("C0KvelBG1Event","GLOBAL",1)
        SetGlobal("EnteredElderCity","GLOBAL",-1)
        ClearAllActions()
        StartCutSceneMode()
        StartCutScene("C0KVC06")
END

IF
	InParty(Myself)
	AreaCheck("%bg1_eet_symbol%0123")
	Dead("C0KVDR02")
	Dead("C0KVDR04")
	Dead("C0KVDR05")
	Global("C0KvelBG1Event","GLOBAL",7)
	CombatCounter(0)
THEN
RESPONSE #100
	SetInterrupt(FALSE)
	Wait(1)
	SetGlobal("C0KvelBG1Event","GLOBAL",8)
	StartDialogueNoSet(Player1)
	SetInterrupt(TRUE)
END

// MARKETPLACE

IF
	InParty(Myself)
	AreaCheck("%bg1_eet_symbol%0700")
	OR(3)
	See("Door0724")
	See("Door0725")
	See("Door0726")
	OR(2)
	!Global("Chapter","GLOBAL",7)
	GlobalGT("DukeThanks","GLOBAL",0)
	Global("C0KvelBG1MarketplaceTalk","GLOBAL",0)
	CombatCounter(0)
THEN
RESPONSE #100
	SetInterrupt(FALSE)
	Wait(1)
	SetGlobal("C0KvelBG1MarketplaceTalk","GLOBAL",1)
	StartDialogueNoSet(Player1)
	SetInterrupt(TRUE)
END

// OXLEY DEAD

IF
	InParty(Myself)
	AreaCheck("%bg1_eet_symbol%0615")
	Global("C0KvelDeadOxley","GLOBAL",1)
	See("C0KVNP05")
	CombatCounter(0)
THEN
RESPONSE #100
	SetInterrupt(FALSE)
	Wait(1)
	SetGlobal("C0KvelDeadOxley","GLOBAL",2)
	StartDialogueNoSet(Player1)
	SetInterrupt(TRUE)
END

// SPIDER'S BANE

IF
	InParty(Myself)
	CombatCounter(0)
	HasItemEquiped("SW2H06",Myself)
	Global("bd_plot","global",0)
	Global("C0KvelBG1SpidersBane","GLOBAL",0)
THEN
RESPONSE #100
	Wait(1)
	SetGlobal("C0KvelBG1SpidersBane","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// TALK 1

IF
    InParty(Myself)
    Global("C0KvelBG1Talk1","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C0KvelBG1TalkRealTimer","GLOBAL",THIRTY_MINUTES)
    SetGlobalTimer("C0KvelBG1TalkTimer","GLOBAL",ONE_DAY)
    SetGlobal("C0KvelBG1Talk1","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C0KvelBG1TalkRealTimer","GLOBAL")
    GlobalTimerExpired("C0KvelBG1TalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
	Global("C0KvelBG1Talk1","GLOBAL",1)
THEN
  RESPONSE #100
	SetGlobal("C0KvelBG1Talk1","GLOBAL",2)
    StartDialogueNoSet(Player1)
END

// TALK 2 TIMER

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0KvelBG1Talk1","GLOBAL",3)
THEN
  RESPONSE #100
	SetGlobal("C0KvelBG1Talk1","GLOBAL",4)
	RealSetGlobalTimer("C0KvelTalk2RealTimer","GLOBAL",3600)
	SetGlobalTimer("C0KvelTalk2Timer","GLOBAL",THREE_DAYS)
END
	